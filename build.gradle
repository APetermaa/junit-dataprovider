plugins {
    id 'com.github.kt3k.coveralls' version '2.3.1'
    id 'de.aaschmid.cpd' version '0.4'
    id 'eclipse'
    id 'groovy'
    id 'jacoco'
    id 'java'
    id 'findbugs'
}

archivesBaseName = 'junit-dataprovider'
description = 'A TestNG like dataprovider runner for JUnit having a simplified syntax compared to all the existing JUnit features.'
group = 'com.tngtech.java'
version = '1.10.2-SNAPSHOT'

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_5

ext {
    isBuildOnJenkins = System.env['BUILD_TAG']?.startsWith('jenkins-') ?: false
}
if (!hasProperty('junitVersion')) { // set default junit version if not set via command line
    ext.junitVersion = '4.12'
}
ext.skipFindBugs = hasProperty('skipFindBugs')
println "Using junit version ${junitVersion} for current build (skipFindBugs = ${skipFindBugs})."

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'junit', name: 'junit', version: junitVersion
    compile group: 'com.google.code.findbugs', name: 'annotations', version: '2.0.1'

    testCompile group: 'org.codehaus.groovy', name: 'groovy', version: '2.0.8'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '1.2.0'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.9.5'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

task jenkinsTest {
    // required due to jenkins fails if test output is not up to date :(
    // Solution found on http://www.practicalgradle.org/blog/2011/06/incremental-tests-with-jenkins/
    inputs.files test.outputs.files
    enabled = isBuildOnJenkins
    doLast {
        def timestamp = System.currentTimeMillis()
        test.getReports().getJunitXml().getDestination().eachFile{ it.lastModified = timestamp }
    }
}
build.dependsOn(jenkinsTest)

jar {
    manifest {
        def name = 'JUnit dataprovider'
        def company = 'TNG Technology Consulting GmbH'

        attributes (
            'Built-By': "Gradle ${gradle.gradleVersion}",
            'Built-Date': new Date(), // TODO destroys incremental build feature
            'Specification-Title': name,
            'Specification-Version': version,
            'Specification-Vendor': company,
            'Implementation-Title': name,
            'Implementation-Version': version,
            'Implementation-Vendor': company,
            'Issue-Tracker': 'https://github.com/TNG/junit-dataprovider/issues',
            'Copyright': "${new Date()[Calendar.YEAR]} ${company}",
            'License': 'Apache License v2.0, January 2004',

            // p2 plugin
            'Bundle-ManifestVersion': '2',
            'Bundle-Name': name,
            'Bundle-Description': project.description,
            'Bundle-SymbolicName': "${project.group}.junit.dataprovider",
            'Bundle-Version': version,
            'Bundle-Vendor': company,
            'Bundle-Copyright': "${new Date()[Calendar.YEAR]} ${company}",
            'Bundle-License': 'Apache License v2.0, January 2004',
            'Require-Bundle': 'org.junit',
            'Import-Package': 'org.osgi.framework',
            'Export-Package': "${project.group}.junit.dataprovider;version=${version}",
        )
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

test {
    ignoreFailures = isBuildOnJenkins
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
    }
}
jacoco {
    toolVersion = '0.7.1.201405082137'
}

findbugs {
    if (System.getProperty("java.version")[0..2] in [ '1.5', '1.6' ]) { // FindBugs v3.0.0 does not work with Java < v1.7 :-(
        toolVersion = '2.0.3'
    } else {
        toolVersion = '3.0.0'
    }
    ignoreFailures = true
}
tasks.withType(FindBugs).each { task ->
    task.enabled = !skipFindBugs
}

cpdCheck {
    ignoreFailures = true
    minimumTokenCount = 25
    source = sourceSets.main.java.srcDirs + sourceSets.test.java.srcDirs // don't do check for groovy
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

// -- build and publish artifacts -------------------------------------------------------------------------------------

apply plugin: 'maven'
apply plugin: 'signing'


signing {
    // requires gradle.properties, see http://www.gradle.org/docs/current/userguide/signing_plugin.html
    required {
        gradle.taskGraph.hasTask('uploadArchives')
    }
    sign configurations.archives
}

if (!hasProperty('sonatypeUsername')) {
	ext.sonatypeUsername = ''
}
if (!hasProperty('sonatypePassword')) {
	ext.sonatypePassword = ''
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { deployment ->
                signing.signPom(deployment)
            }
            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                // username and password from gradle.properties
                authentication(userName: sonatypeUsername, password: sonatypePassword )
            }
            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                authentication(userName: sonatypeUsername, password: sonatypePassword )
            }

            pom {
                project {
                    name 'junit-dataprovider'
                    packaging 'jar'
                    description 'A TestNG like dataprovider runner for JUnit'
                    url 'https://github.com/TNG/junit-dataprovider'

                    scm {
                        url 'scm:git@github.com:TNG/junit-dataprovider.git'
                        connection 'scm:git@github.com:TNG/junit-dataprovider.git'
                        developerConnection 'scm:git@github.com:TNG/junit-dataprovider.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'aaschmid'
                            name 'Andreas Schmid'
                        }
                    }
                }
                whenConfigured { pom ->
                    pom.dependencies.find{ dep -> dep.groupId == 'junit' && dep.artifactId == 'junit' }.with {
                        version = '[4.8.2,4.12]'
                        scope = 'provided'
                    }
                    pom.dependencies.removeAll(pom.dependencies.findAll{ dep -> dep.scope in [ 'compile', 'test' ] })
                }
            }
        }
    }
}
